import os, re, imp

if GetOption('win64'):
    from utils import win_build as c
else:
    from utils import linux_build as c

# Write all default parameters
sourcepaths = c.sourcepaths
libpaths    = c.libpaths
cppflags    = c.cppflags
linkflags   = c.linkflags
lSDL      = c.lSDL
lSDLimage = c.lSDLimage
lSDLmixer = c.lSDLmixer
lSDLttf   = c.lSDLttf
lBullet   = c.lBullet
lGL       = c.lGL
lFlite    = c.lFlite
lNoise    = c.lNoise
lPolyVox  = c.lPolyVox
lOther    = c.lOther
lAll      = c.lAll
lAngelScript  = c.lAngelScript

Import('env')

# List of unit test files to prune away
unitTests = set(Glob('Test*.cpp') + Glob('*/Test*.cpp') + Glob('*/*/Test*.cpp'))
flymakeFiles = set(Glob('*/*_flymake.cpp') + Glob('*/*/*_flymake.cpp'))
env.Append(CPPPATH   = sourcepaths)
env.Append(LIBPATH   = libpaths)
env.Append(CPPFLAGS  = cppflags)
env.Append(CPPPATH   = sourcepaths)
env.Append(LINKFLAGS = linkflags)

swlMain = os.getenv('SWL_MAIN_FILE', 'main.cpp')
swlTargetSuffix = os.getenv('SWL_TARGET_SUFFIX', '')
target = '#/bin/main' + swlTargetSuffix

if GetOption('win64'):
    #############################################
    #           WINDOWS BUILD
    #############################################
    #-------------------------------------------#
    cppFiles = [Glob(swlMain) +
                Glob('extern/*.cpp') +
                Glob('util/*.cpp') +
                Glob('io/*.cpp') +
                Glob('audio/*.cpp') +
                Glob('model/*.cpp') +
                Glob('core/*.cpp') +
                Glob('graphics/*.cpp') +
                Glob('math/*.cpp')]
    app = env.Program(target, cppFiles,
                      LIBPATH = libpaths,
                      CPPFLAGS = cppflags,
                      LINKFLAGS = ('--static -Wl,--no-undefined ' +
                                   '-static-libgcc -static-libstdc++'),
                      LIBS = (lSDL + lSDLmixer + lSDLttf + lSDLimage +
                              lNoise + lPolyVox + lFlite + lBullet +
                              lAngelScript +
                              lGL + lOther))
    Default(app)

else:
    #############################################
    #           LINUX BUILD
    #############################################
    #-------------------------------------------#

    ######################
    # SHARED MATH LIBRARY
    #######################
    mathFiles = [list(set(Glob('math/*.cpp') +
                          Glob('math/*/*.cpp')) - unitTests - flymakeFiles)]

    mathLib = env.SharedLibrary('#/lib/linux/swl-math',
                                mathFiles,
                                LIBS    = [],
                                LIBPATH = []);
    Default(mathLib)


    ######################
    # SHARED EXTERNAL LIBRARY
    #######################
    externFiles = [list(set(Glob('extern/*.cpp') +
                            Glob('extern/*/*.cpp')) - unitTests - flymakeFiles)]
    externLib = env.SharedLibrary('#/lib/linux/swl-extern',
                              externFiles,
                              LIBS     = [],
                              LIBPATH  = []);
    Default(externLib)

    ######################
    # SHARED UTIL LIBRARY
    #######################
    utilFiles = [list(set(Glob('util/*.cpp') +
                          Glob('util/*/*.cpp')) - unitTests - flymakeFiles)]
    utilLib = env.SharedLibrary('#/lib/linux/swl-util',
                                utilFiles,
                                LIBS = externLib + mathLib + lSDL);
    Default(utilLib)

    ######################
    # SHARED AUDIO LIBRARY
    #######################
    audioFiles = [list(set(Glob('audio/*.cpp') +
                           Glob('audio/*/*.cpp')) - unitTests - flymakeFiles)]
    audioLib = env.SharedLibrary('#/lib/linux/swl-audio',
                                 audioFiles,
                                 LIBS = lSDL + lSDLmixer + lFlite + mathLib + utilLib);
    Default(audioLib)


    ######################
    # SHARED GRAPHICS LIBRARY
    #######################
    graphicsFiles = [list(set(Glob('graphics/*.cpp') +
                              Glob('graphics/*/*.cpp')) - unitTests - flymakeFiles)]
    graphicsLib = env.SharedLibrary('#/lib/linux/swl-graphics',
                                    graphicsFiles,
                                    LIBS = mathLib + utilLib + externLib +
                                    lSDL + lSDLimage + lSDLttf +
                                    lGL);
    Default(graphicsLib)


    ######################
    # SHARED IO LIBRARY
    #######################
    ioFiles = [list(set(Glob('io/*.cpp') +
                        Glob('io/*/*.cpp')) - unitTests - flymakeFiles)]
    ioLib = env.SharedLibrary('#/lib/linux/swl-io',
                              ioFiles,
                              LIBS = utilLib + lSDL + mathLib);
    Default(ioLib)


    ######################
    # SHARED MODEL LIBRARY
    #######################
    modelFiles = [list(set(Glob('model/*.cpp') +
                           Glob('model/*/*.cpp')) - unitTests - flymakeFiles)]
    modelLib = env.SharedLibrary('#/lib/linux/swl-model',
                                 modelFiles,
                                 LIBS = mathLib + utilLib + lSDL + lBullet);
    Default(modelLib)


    ######################
    # SHARED CORE LIBRARY
    #######################
    coreFiles = [list(set(Glob('core/*.cpp') +
                          Glob('core/*/*.cpp')) - unitTests - flymakeFiles)]
    coreLib = env.SharedLibrary('#/lib/linux/swl-core',
                                coreFiles,
                                LIBS = mathLib + utilLib + externLib +
                                audioLib + modelLib + ioLib + graphicsLib +
                                lAll)
    Default(coreLib)


    ######################
    # Main Application
    #######################
    cppMain = Glob(swlMain)
    app = env.Program(target, cppMain,
                      LIBPATH  = libpaths,
                      CPPFLAGS = cppflags,
                      LIBS = lAll + mathLib + utilLib + externLib +
                      audioLib + modelLib + ioLib + graphicsLib + coreLib)
    Default(app)
